#!/usr/bin/env bds

helpUnsorted := true

// Relative script (BDS) file path : 
// 	This is important because pipeline seeks for species file 
//		or executables like .py .sh .R in the BDS script path if they are not defined globally in env. var. $PATH
// 	Add more relative path here if you want to keep your .py .sh .R in subdirectories (or parent) of your BDS script directory.

hostname 	:= get_hostname()
rel_script_file_paths 	:= [".","..","modules","../chipseq/modules"]
rel_species_file_paths 	:= [".","..","species","../chipseq/species"]


// command line argument functions

bool cmd_line_arg_has_key( string key ) {

	key = key.toLower()

	for ( string arg : args ) {
		if ( ("-"+key) == arg.toLower().trim() ) return true
	}
	return false
}

bool is_cmd_line_arg_empty() {

	return args.size()==0
}

bool is_first_arg_conf() {

	if ( (args.size()>0) && (!args[0].startsWith("-")) )  {

		if ( args.size()==1 ) {
			return true
		}
		else {
			return args[1].startsWith("-")
		}
	}
	return false
}

string get_cmd_line_arg_val( string key ) {

	key = key.toLower()

	for (int i=0; i< args.size(); i++) {

		arg := args[i]
		
		if ( ("-"+key) == arg.toLower().trim() ) {

			if ( i==(args.size()-1) ) break
			next_arg := args[i+1]

			if ( next_arg.startsWith("-") ) break

			return next_arg
		}
	}
	return ""
}

// string functions

string get_basename( string path ) { // BDS .baseName() returns wrong basename for softlinks
	if (path=="") return ""
	int loc_slash = path.lastIndexOf( "/" )
	return path.substr( loc_slash+1 )
}

string[] get_basename( string[] paths ) { // BDS .baseName() returns wrong basename for softlinks
	string[] ret
	for ( string path : paths ) {
		if (path=="") { 
			ret.add("")
			continue
		}
		int loc_slash = path.lastIndexOf( "/" )
		ret.add( path.substr( loc_slash+1 ) )
	}
	return ret
}

string get_common_basename( string f1, string f2 ) {

	base1 := get_basename( f1 )
	base2 := get_basename( f2 )

	string common

	for (int i=0; i< min(base1.length(),base2.length());i++) {
		c1 := base1.substr(i,i+1)
		c2 := base2.substr(i,i+1)

		if ( c1 == c2 ) common = common + c1
		else break
	}

	common = rm_str_at_end( common, "_" )
	common = rm_str_at_end( common, "." )
	common = rm_str_at_end( common, "-" )

	return common
}

string remove_ext( string file, string ext ) {
	// remove .gz
	if ( file.endsWith( ".gz" ) ) \
		file = rm_str_at_end( file, ".gz" )
	// remove dot from ext
	ext = ext.replace( ".", "" );
	file = rm_str_at_end( file, "."+ext )

	return file
}

string remove_ext( string file, string[] exts ) {
	// remove .gz
	if ( file.endsWith( ".gz" ) ) \
		file = rm_str_at_end( file, ".gz" )

	for ( string ext : exts ) {
		// remove dot from ext
		ext = ext.replace( ".", "" );
		file = file.rm_str_at_end( "."+ext )
	}

	return file
}

string concat_basename_wo_ext( string file1, string file2, string[] exts1, string[] exts2 ) {
	base1_wo_ext := remove_ext( get_basename( file1 ), exts1 )
	base2_wo_ext := remove_ext( get_basename( file2 ), exts2 )	
	return base1_wo_ext + "_" + base2_wo_ext
}

string concat_basename_wo_ext( string file1, string file2, string[] exts ) {
	return concat_basename_wo_ext( file1, file2, exts, exts )
}

string concat_basename_wo_ext( string file1, string file2, string ext1, string ext2 ) {
	return concat_basename_wo_ext( file1, file2, [ext1], [ext2] )
}

string concat_basename_wo_ext( string file1, string file2, string ext ) {
	return concat_basename_wo_ext( file1, file2, [ext], [ext] )
}

string make_vs_basename_wo_gz( string file1, string file2, string suffix ) {
	ret := rm_str_at_end( get_basename( file1 ),".gz" ) + \
		"_VS_" + rm_str_at_end( get_basename( file2 ),".gz" )

	// linux filename length limitation is 255
	if ( ret.length() > 210 ) {
		ret = ret.substr(0, 210)
	}

	ret = ret + suffix

	return ret
}

string make_x_basename_wo_gz( string file1, string file2, string suffix ) {
	ret := rm_str_at_end( get_basename( file1 ),".gz" ) + \
		"_x_" + rm_str_at_end( get_basename( file2 ),".gz" )

	// linux filename length limitation is 255
	if ( ret.length() > 210 ) {
		ret = ret.substr(0, 210)
	}

	ret = ret + suffix

	return ret
}

int parse_time( string str ) {
	trimmed := str.trim().toLower()

	if (  trimmed.indexOf("w")>=0 || trimmed.indexOf("d")>=0 || trimmed.indexOf("h")>=0 \
		|| trimmed.indexOf("m")>=0 || trimmed.indexOf("s")>=0 ) {

		int multiplier
		string delimiter

		if ( trimmed.indexOf("w")>=0 ) {
			delimiter = "w";        multiplier = 3600*24*7
		}
		else if ( trimmed.indexOf("d")>=0 ) {
			delimiter = "d";        multiplier = 3600*24
		}
		else if ( trimmed.indexOf("h")>=0 ) {
			delimiter = "h";        multiplier = 3600
		}
		else if ( trimmed.indexOf("m")>=0 ) {
			delimiter = "m";        multiplier = 60
		}
		else if ( trimmed.indexOf("s")>=0 ) {
			delimiter = "s";        multiplier = 1
		}

		string[] arr = trimmed.split( delimiter )
		return arr[0].parseInt()*multiplier
	}
	else if ( trimmed.indexOf(":")>=0 ) {
		string[] arr = trimmed.split( ":" )

		int d, h, m, s

		if ( arr.size() >= 4 ) {
			d = arr[0].parseInt(); h = arr[1].parseInt(); m = arr[2].parseInt(); s = arr[3].parseInt()
		}
		else if ( arr.size() >= 3 ) {
			h = arr[0].parseInt(); m = arr[1].parseInt(); s = arr[2].parseInt()
		}
		else if ( arr.size() >= 2 ) {
			m = arr[0].parseInt(); s = arr[1].parseInt()
		}
		else if ( arr.size() >= 1 ) {
			s = arr[0].parseInt()
		}

		return 3600*24*d + 3600*h + 60*m + s
	}
	else {
		return trimmed.parseInt()
	}
}

int parse_mem( string str ) {
	trimmed := str.trim().toLower()

	int multiplier
	string delimiter

	if ( trimmed.indexOf("gb")>=0 ) {
		delimiter = "gb"
		multiplier = 1000*1000*1000
	}
	else if ( trimmed.indexOf("g")>=0 ) {
		delimiter = "g"
		multiplier = 1000*1000*1000
	}
	else if ( trimmed.indexOf("mb")>=0 ) {
		delimiter = "mb"
		multiplier = 1000*1000
	}
	else if ( trimmed.indexOf("m")>=0 ) {
		delimiter = "m"
		multiplier = 1000*1000
	}
	else if ( trimmed.indexOf("kb")>=0 ) {
		delimiter = "kb"
		multiplier = 1000
	}
	else if ( trimmed.indexOf("k")>=0 ) {
		delimiter = "k"
		multiplier = 1000
	}
	else if ( trimmed.indexOf("b")>=0 ) {
		delimiter = "b"
		multiplier = 1
	}
	else {
		return trimmed.parseInt()
	}

	string[] arr = trimmed.split( delimiter )
	return arr[0].parseInt()*multiplier
}

int parse_int( string str ) {

	return str.trim().parseInt()
}

bool parse_bool( string str ) {

	return str.trim().parseBool()
}

real parse_real( string str ) {

	return str.trim().parseReal()
}

string rm_comment( string str ) { // remove substring after double slash "//"

	trimmed := str.trim()

	int pos = trimmed.indexOf("//")

	if ( pos==0 ) {
		trimmed = trimmed.substr(0,pos).trim()
	}

	int pos2 = trimmed.indexOf("#")

	if ( pos2>=0 ) {
		trimmed = trimmed.substr(0,pos2).trim()
	}

	int pos3 = trimmed.indexOf(" //")

	if ( pos3>=0 ) {
		trimmed = trimmed.substr(0,pos3).trim()
	}

	int pos4 = trimmed.indexOf("\t//")

	if ( pos4>=0 ) {
		trimmed = trimmed.substr(0,pos4).trim()
	}

	return trimmed
}

string rm_str_at_end( string str, string rm ) {
	trimmed := str.trim()
	
	if ( trimmed.substr( trimmed.length()-rm.length() ).toLower() == rm.toLower() ) {
		return trimmed.substr( 0, trimmed.length()-rm.length() )
	}
	
	return trimmed
}

string array_to_str( string[] arr, string delim ) {
	string ret
	for ( string str : arr ) ret = ret + str + delim	
	return ret
}

string array_to_str( string[] arr ) {
	return array_to_str( arr, "\n")
}

string[] concat( string[] s1, string[] s2 ) {
	string[] ret	
	for ( string str : s1 ) ret.add(str)
	for ( string str : s2 ) ret.add(str)
	return ret
}

string[] concat( string[] s1, string[] s2, string[] s3 ) {
	string[] ret	
	for ( string str : s1 ) ret.add(str)
	for ( string str : s2 ) ret.add(str)
	for ( string str : s3 ) ret.add(str)
	return ret
}

string[] concat( string[] s1, string[] s2, string[] s3, string[] s4 ) {
	string[] ret	
	for ( string str : s1 ) ret.add(str)
	for ( string str : s2 ) ret.add(str)
	for ( string str : s3 ) ret.add(str)
	for ( string str : s4 ) ret.add(str)
	return ret
}

bool is_in_array( string str, string[] arr ) { // case-insensitive
	for( string val : arr ) \
		if ( val.toLower() == str.toLower() ) \
			return true
	return false
}

string change_dir( string file, string out_dir ) {
	return out_dir + "/" + get_basename( file )
}

string get_path( string str ) {
	base := rm_str_at_end( str, "/" ).path()
	return base
}

string mkdir_path( string str ) { 
	// make filename full path and mkdir -p
	path := get_path( str )

	if ( path.exists() ) {		
		return path
	}
	else {
		path.mkdir()
		return path
	}
}

bool path_exists( string path ) {
	if ( path!="" ) {
		if ( path.exists() ) {
			if ( path.isFile() ) {
				if ( path.size() > 0 ) return true
			}
			else {
				return true
			}
		}
	}
	return false
}

string[] get_script_file_paths( string suffix ) {
	script_file 	:= "$ppwd/$programPath"	// path of your BDS script

	if (!script_file.exists()) script_file = "$programPath"

	script_path := script_file.dirName()

	string[] ret

	for ( string path : rel_script_file_paths ) {
		path = "$script_path/$path"
		if ( path.exists() ) ret.add( path + suffix )
	}

	return ret
}

string[] get_script_file_paths() {
	return get_script_file_paths( "" )
}


string[] get_species_file_paths( string suffix ) {
	species_file 	:= "$ppwd/$programPath"	// path of your BDS species

	if (!species_file.exists()) species_file = "$programPath"

	species_path := species_file.dirName()

	string[] ret

	for ( string path : rel_species_file_paths ) {
		path = "$species_path/$path"
		if ( path.exists() ) ret.add( path + suffix )
	}

	return ret
}

string[] get_species_file_paths() {
	return get_species_file_paths( "" )
}

void save_data( string file, string[] data ) { // ; separated array
	string str
	for( string val : data ) { str = str + val + ";" }

	rm_str_at_end( str, ";")
	file.write( str )
}

void save_data( string file, string[] data, string delimiter ) { // ; separated array
	string str
	for( string val : data ) { str = str + val + delimiter }

	rm_str_at_end( str, delimiter )
	file.write( str )

	sleep( 3 ) // wait 3 seconds before 	
}

string[] load_data( string file ) {
	sleep( 3 ) // wait 3 seconds before loading t

	return file.read().split(";")
}

string get_stdout( string cmd ) {
	cmd_ := "cmd_"+randInt()
	sys $cmd 1> $cmd_
	wait
	ret := cmd_.read()
	cmd_.delete()
	return ret
}

string get_shell_var( string var ) {
	var_ := "var_$var"
	sys echo "${$var}" > $var_
	wait
	ret := var_.read()
	var_.delete()
	return ret
}

int get_no_lines( string file ) {
	
	if ( !path_exists( file ) ) {
		error("get_no_lines(): File doesn't exist! ($file)")
	}
	else {
		// check if compressed or not

		if ( file.toLower().endsWith(".gz") ) {
			return get_stdout( "zcat $file | wc -l" ).parseInt()
		}
		else {
			return get_stdout( "cat $file | wc -l" ).parseInt()
		}
	}
}

string get_hostname() { 
	// get full hostname including domain name (http://serverfault.com/questions/367141/how-to-get-the-fully-qualified-name-fqn-on-unix-in-a-bash-script)
	return get_stdout("fqn=$(host -TtA $(hostname)|grep 'has address'|awk '{print $1}'); if [[ '${fqn}' == '' ]] ; then fqn=$(hostname) ; fi ; echo ${fqn}").replace("\n","")
}

bool is_system_sge() {

	return system == "sge"
}

bool is_system_local() {

	return system == "local"
}

bool is_system_generic() {

	return system == "generic"
}

bool is_scg3() {

	name := hostname.toLower()

	return  name.endsWith( ".stanford.edu" ) && \
		( name.startsWith( "carmack" ) || name.startsWith( "crick" ) || name.startsWith( "scg3" ) )
}

bool is_kundaje_lab() {

	name := hostname.toLower()

	hostname_kundaje:= ["nandi.stanford.edu","mitra.stanford.edu","kali.stanford.edu","vayu.stanford.edu",\
			    "wotan.stanford.edu","durga.stanford.edu","amold.stanford.edu","dhara.stanford.edu"]

	for ( string name_ : hostname_kundaje ) {
		if ( name == name_ ) return true
	}
	return false
}
