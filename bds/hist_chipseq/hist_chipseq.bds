#!/usr/bin/env bds

include "../modules_align_map.bds"


// cmd line arg wrapper and help
string gensz 		= ""				help Genome size; hs for human, mm for mouse (default: hs)
int nth_macs2		= 2 				help Number of threads for MACS2 (default: 2)

// parameters
string GENOMESIZE 	= gensz
int NTHREADS_MACS2 	= nth_macs2




// pipeline starts here
read_param_general()
read_param_align_map()
read_param_hist_chipseq()

align_map_xcor_spr_pspr()

macs2()


// functions
void read_param_hist_chipseq() {

	// read parameters
	if ( conf_file_exists() ) {
		
		if ( conf.hasKey("GENOMESIZE") ) 		GENOMESIZE 		= rm_comment( conf{ "GENOMESIZE" } )
		if ( conf.hasKey("NTHREADS_MACS2") )	NTHREADS_MACS2	= parse_int( conf{ "NTHREADS_MACS2" } )
	}

	print("GENOMESIZE = $GENOMESIZE\n");
	print("NTHREADS_MACS2 = $NTHREADS_MACS2\n");

}

void macs2() {

	if ( QC_ONLY ) return

	print( "\nMACS2...\n" );

	FRAGLEN := get_fraglen( 0, 1 ) // get it from rep1
	print("FRAGLEN for MACS2: $FRAGLEN \n")
	
	// prepare for tagAlign files
	fastqs_Rep1 	:= get_fastqs( 0, 1 )
	fastqs_Rep2 	:= get_fastqs( 0, 2 ) // check if rep2 doesn't exist
	suffix_PE 		:= ".filt.srt.nodup.PE2SE.tagAlign.gz"
	suffix_SE 		:= ".filt.nodup.srt.SE.tagAlign.gz"
	suffix 			:= fastqs_Rep1.size()==2 ? suffix_PE : suffix_SE
	suffix_PSR_PE 	:= ".filt.nodup.PE2SE"
	suffix_PSR_SE 	:= ".filt.nodup.SE"
	suffix_PSR 		:= fastqs_Rep1.size()==2 ? suffix_PSR_PE : suffix_PSR_SE

	DATASET_PREFIX := get_ofprefix( 0 )
	
	REP1_TA_FILE :="$DATASET_PREFIX"+"_Rep1" + suffix
	REP2_TA_FILE :="$DATASET_PREFIX"+"_Rep2" + suffix
	POOLED_TA_FILE :="$DATASET_PREFIX"+"_Rep0.tagAlign.gz"
	REP1_PR1_TA_FILE :="$DATASET_PREFIX"+"_Rep1" + suffix_PSR + ".pr1.tagAlign.gz"
	REP1_PR2_TA_FILE :="$DATASET_PREFIX"+"_Rep1" + suffix_PSR + ".pr2.tagAlign.gz"
	REP2_PR1_TA_FILE :="$DATASET_PREFIX"+"_Rep2" + suffix_PSR + ".pr1.tagAlign.gz"
	REP2_PR2_TA_FILE :="$DATASET_PREFIX"+"_Rep2" + suffix_PSR + ".pr2.tagAlign.gz"
	PPR1_TA_FILE :="$DATASET_PREFIX"+"_Rep0.pr1.tagAlign.gz"
	PPR2_TA_FILE :="$DATASET_PREFIX"+"_Rep0.pr2.tagAlign.gz"

	ctl_fastqs_Rep1 	:= get_fastqs( 1, 1 )
	ctl_fastqs_Rep2 	:= get_fastqs( 1, 2 ) // check if rep2 doesn't exist
	ctl_suffix_PE 		:= ".filt.srt.nodup.PE2SE.tagAlign.gz"
	ctl_suffix_SE 		:= ".filt.nodup.srt.SE.tagAlign.gz"
	ctl_suffix 			:= ctl_fastqs_Rep1.size()==2 ? ctl_suffix_PE : ctl_suffix_SE
	ctl_suffix_PSR_PE 	:= ".filt.nodup.PE2SE"
	ctl_suffix_PSR_SE 	:= ".filt.nodup.SE"
	ctl_suffix_PSR 		:= ctl_fastqs_Rep1.size()==2 ? ctl_suffix_PSR_PE : ctl_suffix_PSR_SE

	CTL_DATASET_PREFIX := get_ofprefix( 1 )

	CTL_REP1_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep1" + ctl_suffix
	CTL_REP2_TA_FILE := ctl_fastqs_Rep2.size()>0 ? "$CTL_DATASET_PREFIX"+"_Rep2" + ctl_suffix : CTL_REP1_TA_FILE
	CTL_POOLED_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep0.tagAlign.gz"
	CTL_REP1_PR1_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep1" + ctl_suffix_PSR + ".pr1.tagAlign.gz"
	CTL_REP1_PR2_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep1" + ctl_suffix_PSR + ".pr2.tagAlign.gz"
	CTL_REP2_PR1_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep2" + ctl_suffix_PSR + ".pr1.tagAlign.gz"
	CTL_REP2_PR2_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep2" + ctl_suffix_PSR + ".pr2.tagAlign.gz"
	CTL_PPR1_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep0.pr1.tagAlign.gz"
	CTL_PPR2_TA_FILE :="$CTL_DATASET_PREFIX"+"_Rep0.pr2.tagAlign.gz"

	PEAK_OUTPUT_DIR := OUTPUT_DIR + "/peaks"
	PEAK_OUTPUT_DIR.mkdir() 

	///////////////////////////////// MACS2

	run_macs2( "Rep1", PEAK_OUTPUT_DIR, 		REP1_TA_FILE, CTL_REP1_TA_FILE, FRAGLEN )
	run_macs2( "Rep2", PEAK_OUTPUT_DIR, 		REP2_TA_FILE, CTL_REP2_TA_FILE, FRAGLEN )
	run_macs2( "Pooled", PEAK_OUTPUT_DIR, 	POOLED_TA_FILE, CTL_POOLED_TA_FILE, FRAGLEN )
	run_macs2( "Rep1_PR1", PEAK_OUTPUT_DIR, 	REP1_PR1_TA_FILE, CTL_REP1_PR1_TA_FILE, FRAGLEN )
	run_macs2( "Rep1_PR2", PEAK_OUTPUT_DIR, 	REP1_PR2_TA_FILE, CTL_REP1_PR2_TA_FILE, FRAGLEN )
	run_macs2( "Rep2_PR1", PEAK_OUTPUT_DIR, 	REP2_PR1_TA_FILE, CTL_REP2_PR1_TA_FILE, FRAGLEN )
	run_macs2( "Rep2_PR2", PEAK_OUTPUT_DIR, 	REP2_PR2_TA_FILE, CTL_REP2_PR2_TA_FILE, FRAGLEN )
	run_macs2( "PPR1", PEAK_OUTPUT_DIR, 		PPR1_TA_FILE, CTL_PPR1_TA_FILE, FRAGLEN )
	run_macs2( "PPR2", PEAK_OUTPUT_DIR, 		PPR2_TA_FILE, CTL_PPR2_TA_FILE, FRAGLEN )

	wait

}

void run_macs2( string taskname, string out_dir, string ta, string ctl_ta, string fraglen ) {

	REP1_PEAK_FILE		:= "$out_dir/" + taskname +".narrowPeak.gz"
 	REP1_BPEAK_FILE 	:= "$out_dir/" + taskname +".broadPeak.gz"
 	REP1_GPEAK_FILE 	:= "$out_dir/" + taskname +".gappedPeak.gz"
 	FC_SIGNAL_BW_FILE 	:= "$out_dir/" + taskname +".fc.signal.bw"
 	PVAL_SIGNAL_BW_FILE	:= "$out_dir/" + taskname +".pval.signal.bw"
 	
	task( taskName := "macs2 " + taskname, cpus := NTHREADS_MACS2, timeout := WALLTIME, mem := MEMORY * M, \
		[REP1_PEAK_FILE, REP1_BPEAK_FILE, REP1_GPEAK_FILE, FC_SIGNAL_BW_FILE, PVAL_SIGNAL_BW_FILE] <- [ta, ctl_ta] ) {

		sys $PRELOAD

		//==========================================
		//# Generate narrow peaks and preliminary signal tracks
		//============================================

		sys macs2 callpeak -t $ta -c $ctl_ta -f BED -n $out_dir/$taskname -g $GENOMESIZE -p 1e-2 --nomodel --shift 0 --extsize $fraglen --keep-dup all -B --SPMR

		//# Sort by Col8 in descending order and replace long peak names in Column 4 with Peak_<peakRank>
		sys sort -k 8gr,8gr $out_dir/"$taskname"_peaks.narrowPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $out_dir/$taskname.narrowPeak.gz

		//# remove additional files
		//#rm -f $out_dir/"$taskname"_peaks.xls #$out_dir/$taskname_peaks.bed ${peakFile}_summits.bed
		sys rm -f $out_dir/"$taskname"_peaks.xls $out_dir/"$taskname"_peaks.narrowPeak $out_dir/"$taskname"_summits.bed

		//===========================================
		//# Generate Broad and Gapped Peaks
		//============================================
		sys macs2 callpeak -t $ta -c $ctl_ta -f BED -n $out_dir/$taskname -g $GENOMESIZE -p 1e-2 --broad --nomodel --shift 0 --extsize $fraglen --keep-dup all


		//# Sort by Col8 (for broadPeak) or Col 14(for gappedPeak)  in descending order and replace long peak names in Column 4 with Peak_<peakRank>
		sys sort -k 8gr,8gr $out_dir/"$taskname"_peaks.broadPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $out_dir/$taskname.broadPeak.gz

		sys sort -k 14gr,14gr $out_dir/"$taskname"_peaks.gappedPeak | awk 'BEGIN{OFS="\t"}{$4="Peak_"NR ; print $0}' | gzip -c > $out_dir/$taskname.gappedPeak.gz

		//# remove additional files
		//#rm -f $out_dir/"$taskname"_peaks.xls #$out_dir/$taskname_peaks.bed ${peakFile}_summits.bed
		sys rm -f $out_dir/"$taskname"_peaks.xls \
		  $out_dir/"$taskname"_peaks.broadPeak \
		  $out_dir/"$taskname"_peaks.gappedPeak \
		  $out_dir/"$taskname"_summits.bed

		//===========================================
		//# For Fold enrichment signal tracks
		//============================================
		//sys CHRSIZEFILE=<path_of_file_containing_chromosome_sizes>
		//# This file is a tab delimited file with 2 columns Col1 (chromosome name), Col2 (chromosome size in bp).

		sys macs2 bdgcmp -t $out_dir/"$taskname"_treat_pileup.bdg -c $out_dir/"$taskname"_control_lambda.bdg --outdir $out_dir -o "$taskname"_FE.bdg -m FE

		//# Remove coordinates outside chromosome sizes (stupid MACS2 bug)
		sys slopBed -i $out_dir/"$taskname"_FE.bdg -g $CHROM_SIZES -b 0 |   awk '{if ($3 != -1) print $0}' |  bedClip stdin $CHROM_SIZES $out_dir/$taskname.fc.signal.bedgraph

		sys rm -f $out_dir/"$taskname"_FE.bdg

		//# Convert bedgraph to bigwig
		sys bedGraphToBigWig $out_dir/$taskname.fc.signal.bedgraph $CHROM_SIZES $out_dir/$taskname.fc.signal.bw

		sys rm -f $out_dir/$taskname.fc.signal.bedgraph

		//===========================================
		//# For -log10(p-value) signal tracks
		//============================================

		//# Compute sval = min(no. of reads in ChIP, no. of reads in control) / 1,000,000

		//#chipReads=$(wc -l $ta | awk '"'"'{printf "%f", $1/1000000}'"')"
		sys chipReads=$(zcat $ta | wc -l | awk '{printf "%f", $1/1000000}');



		//#controlReads=$(wc -l $ctl_ta | awk '"'"'{printf "%f", $1/1000000}'"')
		sys controlReads=$(zcat $ctl_ta | wc -l | awk '{printf "%f", $1/1000000}');

		//#sval=$(echo "${chipReads} ${controlReads}" | awk '"'"'$1>$2{printf "%f",$2} $1<=$2{printf "%f",$1}'"')
		sys sval=$(echo "${chipReads} ${controlReads}" | awk '$1>$2{printf "%f",$2} $1<=$2{printf "%f",$1}');

		sys macs2 bdgcmp -t $out_dir/"$taskname"_treat_pileup.bdg -c $out_dir/"$taskname"_control_lambda.bdg --outdir $out_dir -o "$taskname"_ppois.bdg -m ppois -S "${sval}"

		//# Remove coordinates outside chromosome sizes (stupid MACS2 bug)
		sys slopBed -i $out_dir/"$taskname"_ppois.bdg -g $CHROM_SIZES -b 0 |   awk '{if ($3 != -1) print $0}' |  bedClip stdin $CHROM_SIZES $out_dir/$taskname.pval.signal.bedgraph

		sys rm -rf $out_dir/"$taskname"_ppois.bdg

		//# Convert bedgraph to bigwig
		sys bedGraphToBigWig $out_dir/$taskname.pval.signal.bedgraph $CHROM_SIZES $out_dir/$taskname.pval.signal.bw

		sys rm -f $out_dir/$taskname.pval.signal.bedgraph
		sys rm -f $out_dir/"$taskname"_treat_pileup.bdg ${peakFile}_control_lambda.bdg

	}
}
