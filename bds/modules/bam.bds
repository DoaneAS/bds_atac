#!/usr/bin/env bds

include "species.bds"

// has functions related to bam, and helps getting bam from configruation file or command line argument

help Bam definition
help      Single-ended : To define bam use '-bam[REP_NO]', For control use '-ctl_bam[REP_NO]'
help      Paired end : To define bam use '-bam[REP_NO] -bam[REP_NO]_PE', For control use '-ctl_bam[REP_NO]_[REP_NO] -ctl_bam[REP_NO]_PE'

// Dummy variables for HELP context (bam)
bam1 		:= ""		help Bam or nodup_bam for replicate 1.
bam2 		:= ""		help Bam or nodup_bam for replicate 2.
bam1_PE 	:= false 	help Add it if bam replicate 1 is paired end (default: false).
bam2_PE 	:= false 	help Add it if bam replicate 2 is paired end (default: false).

help Alignment settings for bam

mapq_thresh 	:= 30 		help Threshold for low MAPQ reads removal (default: 30).
rm_chr_from_tag	:= "" 		help If defined, remove lines including it from all tagalign files. (example: 'other|ribo|mito|_', '_', default: blank)

/*
IMPORTANT!: legacy softwares compatibility (make the code work with legacy samtools, picard-tools)

	- samtools fixmate -O doesn't work in legacy samtools
		specify output ext like [TARGET].bam (samtools fixmate -r [SRC] [TARGET].bam)

	- samtools index [SRC] [TARGET] doesn't work in legacy samtools
		you need to manually mv .bam.bai .bai

	- samtools sort -O -T  doesn't work in legacy samtools (do not use them)

	- java -Xmx4G -Xms256M -jar ${PICARDROOT}/picard.jar [METHOD]  for old picard
	- java -Xmx4G -Xms256M -jar ${PICARDROOT}/[METHOD].jar  for new picard
*/


init_bam()


void init_bam() {

	if ( conf_file_exists() ) {
		mapq_thresh 	= get_conf_val_int( mapq_thresh,	["MAPQ_THRESH"] )
		rm_chr_from_tag = get_conf_val( rm_chr_from_tag, 	["RM_CHR_FROM_TAG", "REMOVE_CHR_FROM_TAG", "RM_CHR_FROM_TAGALIGN", "REMOVE_CHR_FROM_TAGALIGN"] )		
	}

	if ( v ) { // verbose
		print("\n")		
		print( "MAPQ reads rm thresh.\t\t: $mapq_thresh\n")
		print( "Rm. tag reads with str.\t\t: $rm_chr_from_tag\n")
	}	
}

string[] _bam_to_nodup_bam( string bam, string out_dir, string info ) {

	prefix 		:= change_dir( remove_ext( bam, "bam" ), out_dir )

	nodup_bam 	:= "$prefix.nodup.bam"
	nodup_bam_idx 	:= "$prefix.nodup.bai"
	dup_qc 		:= "$prefix.dup.qc"
	map_qc 		:= "$prefix.nodup.flagstat.qc"
	pbc_qc 		:= "$prefix.nodup.pbc.qc"
	filt_bam 	:= "$prefix.filt.bam"
	tmp_filt_bam 	:= "$prefix.dupmark.bam"

	in 		:= [ bam ]
	out 		:= [ nodup_bam, nodup_bam_idx, dup_qc, map_qc, pbc_qc ]

	task( taskName:= "bam_to_nodup_bam "+info, \
		cpus:=nth, mem:=parse_mem(mem), timeout:=parse_time(wt), \
		out<-in ) {

		sys $SHCMD_INIT

		//# =============================
		//# Remove  unmapped, mate unmapped
		//# not primary alignment, reads failing platform
		//# Remove low MAPQ reads
		//# ==================  
		sys samtools view -F 1804 -q $mapq_thresh -b $bam > $filt_bam

		//# Mark duplicates
		sys if [ -f ${PICARDROOT}/MarkDuplicates.jar ]; then \
			java -Xmx4G -jar ${PICARDROOT}/MarkDuplicates.jar \
				INPUT="$filt_bam" OUTPUT="$tmp_filt_bam" \
				METRICS_FILE="$dup_qc" VALIDATION_STRINGENCY=LENIENT \
				ASSUME_SORTED=true REMOVE_DUPLICATES=false; \
			else \
			java -Xmx4G -jar ${PICARDROOT}/picard.jar MarkDuplicates \
				INPUT="$filt_bam" OUTPUT="$tmp_filt_bam" \
				METRICS_FILE="$dup_qc" VALIDATION_STRINGENCY=LENIENT \
				ASSUME_SORTED=true REMOVE_DUPLICATES=false; \
			fi

		//# Remove duplicates
		//# Index final position sorted BAM
		sys mv $tmp_filt_bam $filt_bam
		sys samtools view -F 1804 -b $filt_bam > $nodup_bam
		
		//# Index Final BAM file
		sys samtools index $nodup_bam
		
		sys if [ "$nodup_bam.bai" != "$nodup_bam_idx" ]; then \
			cp $nodup_bam.bai $nodup_bam_idx; \
			fi

		sys samtools flagstat $nodup_bam > $map_qc

		//# =============================
		//# Compute library complexity
		//# =============================
		//# sort by position and strand
		//# Obtain unique count statistics

		//# PBC File output
		//# TotalReadPairs [tab] DistinctReadPairs [tab] OneReadPair [tab] TwoReadPairs [tab] NRF=Distinct/Total [tab] PBC1=OnePair/Distinct [tab] PBC2=OnePair/TwoPair
		sys bedtools bamtobed -i $filt_bam | \
			awk 'BEGIN{OFS="\t"}{print $1,$2,$3,$6}' | \
			grep -v 'chrM' | sort | uniq -c | \
			awk 'BEGIN{mt=0;m0=0;m1=0;m2=0} ($1==1){m1=m1+1} ($1==2){m2=m2+1} {m0=m0+1} {mt=mt+$1} END{printf "%d\t%d\t%d\t%d\t%f\t%f\t%f\n",mt,m0,m1,m2,m0/mt,m1/m0,m1/m2}' > $pbc_qc

		sys rm -f $filt_bam
		sys rm -f $tmp_filt_bam
	}

	return out
}

string[] _bam_to_nodup_bam_PE( string bam, string out_dir, string info ) {

	prefix 		:= change_dir( remove_ext( bam, "bam" ), out_dir )

	nodup_bam 	:= "$prefix.nodup.bam"
	nodup_bam_idx 	:= "$prefix.nodup.bai"

	dup_qc 		:= "$prefix.dup.qc"
	map_qc 		:= "$prefix.nodup.flagstat.qc"
	pbc_qc 		:= "$prefix.nodup.pbc.qc"

	filt_bam 	:= "$prefix.filt.bam"
	filt_bam_prefix := "$prefix.filt"

	tmp_filt_bam 	:= "$prefix.dupmark.bam"
	tmp_filt_bam_prefix := "$prefix.dupmark"

	in 		:= [ bam ]
	out 		:= [ nodup_bam, nodup_bam_idx, dup_qc, map_qc, pbc_qc ]

	task( taskName:= "bam_to_nodup_bam_PE "+info, \
		cpus:=nth, mem:=parse_mem(mem), timeout:=parse_time(wt), \
		out<-in ) {
		sys $SHCMD_INIT

		//# =============================
		//# Remove  unmapped, mate unmapped
		//# not primary alignment, reads failing platform
		//# Remove low MAPQ reads
		//# Only keep properly paired reads
		//# Obtain name sorted BAM file
		//# ==================

		sys samtools view -F 1804 -f 2 -q $mapq_thresh -u $bam | \
			samtools sort -n - $tmp_filt_bam_prefix       # Will produce name sorted BAM

		//# Remove orphan reads (pair was removed)
		//# and read pairs mapping to different chromosomes
		//# Obtain position sorted BAM			

		//# Will produce coordinate sorted BAM
		sys samtools fixmate -r $tmp_filt_bam $tmp_filt_bam.fixmate.bam
		sys samtools view -F 1804 -f 2 -u $tmp_filt_bam.fixmate.bam | samtools sort - $filt_bam_prefix
		sys rm $tmp_filt_bam.fixmate.bam

		//# Mark duplicates
		sys if [ -f ${PICARDROOT}/MarkDuplicates.jar ]; then \
			java -Xmx4G -jar ${PICARDROOT}/MarkDuplicates.jar \
				INPUT="$filt_bam" OUTPUT="$tmp_filt_bam" \
				METRICS_FILE="$dup_qc" VALIDATION_STRINGENCY=LENIENT \
				ASSUME_SORTED=true REMOVE_DUPLICATES=false; \
			else \
			java -Xmx4G -jar ${PICARDROOT}/picard.jar MarkDuplicates \
				INPUT="$filt_bam" OUTPUT="$tmp_filt_bam" \
				METRICS_FILE="$dup_qc" VALIDATION_STRINGENCY=LENIENT \
				ASSUME_SORTED=true REMOVE_DUPLICATES=false; \
			fi

		//# ============================
		//# Remove duplicates
		//# Index final position sorted BAM
		//# Create final name sorted BAM
		//# ============================

		sys mv $tmp_filt_bam $filt_bam
		
		sys samtools view -F 1804 -f 2 -b $filt_bam > $nodup_bam
						
		sys samtools index $nodup_bam

		sys if [ "$nodup_bam.bai" != "$nodup_bam_idx" ]; then \
			cp $nodup_bam.bai $nodup_bam_idx; \
			fi

		sys samtools flagstat $nodup_bam > $map_qc

		//# =============================
		//# Compute library complexity
		//# =============================
		//# Sort by name
		//# convert to bedPE and obtain fragment coordinates
		//# sort by position and strand
		//# Obtain unique count statistics
	
		//# TotalReadPairs [tab] DistinctReadPairs [tab] OneReadPair [tab] TwoReadPairs [tab] NRF=Distinct/Total [tab] PBC1=OnePair/Distinct [tab] PBC2=OnePair/TwoPair
		sys samtools sort -n $filt_bam $filt_bam.tmp
		sys bedtools bamtobed -bedpe -i $filt_bam.tmp.bam | \
			awk 'BEGIN{OFS="\t"}{print $1,$2,$4,$6,$9,$10}' | \
			grep -v 'chrM' | sort | uniq -c | \
			awk 'BEGIN{mt=0;m0=0;m1=0;m2=0} ($1==1){m1=m1+1} ($1==2){m2=m2+1} {m0=m0+1} {mt=mt+$1} END{printf "%d\t%d\t%d\t%d\t%f\t%f\t%f\n",mt,m0,m1,m2,m0/mt,m1/m0,m1/m2}' > $pbc_qc
		sys rm $filt_bam.tmp.bam
		
		sys rm -f $filt_bam
		sys rm -f $tmp_filt_bam
	}

	return out
}

string _bam_to_nmsrt_bam( string bam, string out_dir, string info ) {

	prefix 		:= change_dir( remove_ext( bam, "bam" ), out_dir )

	nmsrt_bam 	:= "$prefix.nmsrt.bam"
	nmsrt_bam_prefix:= "$prefix.nmsrt"

	in 		:= [ bam ]
	out 		:= nmsrt_bam

	task( taskName:= "bam_to_nmsrt_bam "+info, \
		cpus:=nth, mem:=parse_mem(mem), timeout:=parse_time(wt), \
		out<-in ) {

		sys $SHCMD_INIT

		sys samtools sort -n $bam $nmsrt_bam_prefix
	}

	return out	
}

string[] _bam_to_srt_bam( string bam, string out_dir, string info ) {

	prefix 		:= change_dir( remove_ext( bam, "bam" ), out_dir )

	srt_bam 	:= "$prefix.srt.bam"
	srt_bam_prefix 	:= "$prefix.srt"
	srt_bam_idx 	:= "$prefix.srt.bai"

	in 		:= [ bam ]
	out 		:= [ srt_bam, srt_bam_idx ]

	task( taskName:= "bam_to_srt_bam "+info, \
		cpus:=nth, mem:=parse_mem(mem), timeout:=parse_time(wt), \
		out<-in ) {

		sys $SHCMD_INIT

		//sys samtools sort -Ttmp -l0 -Obam $bam -o $srt_bam
		//sys samtools index $srt_bam $srt_bam.bai
		//sys mv $srt_bam.bai $srt_bam_idx

		sys samtools sort $bam $srt_bam_prefix
		sys samtools index $srt_bam

		sys if [ "$srt_bam.bai" != "$srt_bam_idx" ]; then \
			cp $srt_bam.bai $srt_bam_idx; \
			fi
		}

	return out	
}

string _bam_to_tag( string bam, string out_dir, string info ) {
	
	prefix 		:= change_dir( remove_ext( bam, "bam" ), out_dir )

	tag 		:= "$prefix.tagAlign.gz"

	in 		:= [ bam ]
	out 		:= tag

	RM_CHR_ := "echo"	

	if ( rm_chr_from_tag != "" ) {
		RM_CHR_ = "zcat $tag | grep -P -v '$rm_chr_from_tag' | gzip -c > $prefix.rm_chr.tmp.gz; mv $prefix.rm_chr.tmp.gz $tag"
	}

	task( taskName:= "bam_to_tag "+info, \
		cpus:=nth, mem:=parse_mem(mem), timeout:=parse_time(wt), \
		out<-in ) {

		sys $SHCMD_INIT
	
		//# Create SE tagAlign file
		sys bedtools bamtobed -i $bam | awk 'BEGIN{OFS="\t"}{$4="N";$5="1000";print $0}' | gzip -c > $tag

		sys $RM_CHR_
	}

	return out
}

string _bam_to_bedpe( string nmsrt_bam, string out_dir, string info ) {

	prefix 		:= change_dir( remove_ext( nmsrt_bam, "bam" ), out_dir )

	bedpe 		:= "$prefix.bedpe.gz"

	in 		:= [ nmsrt_bam ]
	out 		:= bedpe

	task( taskName:= "bam_to_bedpe "+info, \
		cpus:=nth, mem:=parse_mem(mem), timeout:=parse_time(wt), \
		out<-in ) {

		sys $SHCMD_INIT

		//# Create BEDPE file
		sys bedtools bamtobed -bedpe -mate1 -i $nmsrt_bam | gzip -c > $bedpe
	}

	return out
}

string _bam_to_bed_non_chrM( string nodup_bam, string out_dir, string info ) {

	prefix 		:= change_dir( remove_ext( nodup_bam, "bam" ), out_dir )

	mito_bam 	:= "$prefix.chrM.bam"
	non_mito_bam 	:= "$prefix.nonchrM.bam"
	non_mito_bam_hist_grp := "$non_mito_bam.hist_graph.pdf"
	non_mito_bam_hist_log := "$non_mito_bam.hist_data.log"

	bed 	:= "$prefix.nonchrM.tn5.bed.gz"

	in 	:= [nodup_bam]
	out 	:= bed //, mito_bam, non_mito_bam, non_mito_bam_hist_grp ]

	task( taskName:= "bam_to_bed_non_chrM "+info, \
		cpus:=nth, mem:=parse_mem(mem), timeout:=parse_time(wt), \
		out<-in ) {

		sys $SHCMD_INIT

		sys nonMitoChromosomes=$(samtools view -H "$nodup_bam" | \
					grep chr | cut -f2 | sed 's/SN://g' | grep -v chrM)

		sys samtools view -b "$nodup_bam" ${nonMitoChromosomes} > "$non_mito_bam"		
		sys samtools view -b "$nodup_bam" chrM > "$mito_bam"

		//# Process only non chrM reads
		// embedded adjustBedTn5.sh
		sys bamToBed -i "$non_mito_bam" | \
			awk -F $'\t' 'BEGIN {OFS = FS}{ if ($6 == "+") {$2 = $2 + 4} else if ($6 == "-") {$3 = $3 - 5} print $0}' | \
			gzip -c > "$bed"

		sys if [ -f ${PICARDROOT}/CollectInsertSizeMetrics.jar ]; then \
				java -Xmx4G -Xms256M -jar ${PICARDROOT}/CollectInsertSizeMetrics.jar \
				INPUT="$non_mito_bam" OUTPUT="$non_mito_bam_hist_log" \
				H="$non_mito_bam_hist_grp" W=1000 STOP_AFTER=5000000; \
			else \
				java -Xmx4G -Xms256M -jar ${PICARDROOT}/picard.jar CollectInsertSizeMetrics \
				INPUT="$non_mito_bam" OUTPUT="$non_mito_bam_hist_log" \
				H="$non_mito_bam_hist_grp" W=1000 STOP_AFTER=5000000; \
			fi
	}

	return out
}

string get_bam( int ctl, int rep ) {

	key := ( ctl > 0 ? "ctl_bam" : "bam" ) + "_rep" + rep
	key2 := ( ctl > 0 ? "ctl_bam" : "bam" ) + rep
	key3 := ( ctl > 0 ? "ctl_bam" : "bam" )

	if ( (ctl==0) && (rep==1) && ( bam1 != "") ) {
		return bam1
	}
	else if ( (ctl==0) && (rep==2) && ( bam2 != "") ) {
		return bam2
	}
	else if ( cmd_line_arg_has_key( key ) ) {
		return get_cmd_line_arg_val( key )
	}
	else if ( cmd_line_arg_has_key( key2 ) ) {
		return get_cmd_line_arg_val( key2 )
	}
	else if ( (rep==1) && cmd_line_arg_has_key( key3 ) ) {
		return get_cmd_line_arg_val( key3 )
	}
	else if ( conf.hasKey( key ) ) {
		return get_path( conf{ key } )
	}
	else if ( conf.hasKey( key2 ) ) {
		return get_path( conf{ key2 } )
	}
	else if ( (rep==1) && conf.hasKey( key3 ) ) {
		return get_path( conf{ key3 } )
	}
	return ""
}

bool is_bam_paired_end( int ctl, int rep ) {

	key := ( ctl > 0 ? "ctl_bam" : "bam" ) + rep + "_pe"
	key_wo_rep := ( ctl > 0 ? "ctl_bam" : "bam" ) + "_pe"

	if ( conf.hasKey( key ) ) {
		return conf{ key }.parseBool()
	}
	else if ( (rep==1) && conf.hasKey( key_wo_rep ) ) {
		return conf{ key_wo_rep }.parseBool()
	}
	else if ( cmd_line_arg_has_key( key ) ) {
		val := get_cmd_line_arg_val( key )
		return (val=="") || (val.parseBool())
	}
	else if ( (rep==1) && cmd_line_arg_has_key( key_wo_rep ) ) {
		val := get_cmd_line_arg_val( key_wo_rep )
		return (val=="") || (val.parseBool())
	}	
	return false
}

bool is_bam_single_ended( int ctl, int rep ) {

	return !is_bam_paired_end( ctl, rep )
}

bool is_bam_PE( int ctl, int rep ) {

	return is_bam_paired_end( ctl, rep )
}

bool is_bam_SE( int ctl, int rep ) {

	return is_bam_single_ended( ctl, rep )
}
