#!/usr/bin/env bds

include "align.bds"


help Alignment settings : bwa

param_bwa_aln	:= "-q 5 -l 32 -k 2" 	help Parameters for bwa aln (default: "-q 5 -l 32 -k 2").
nth_bwa_aln	:= 2 			help # threads for bwa aln (default: 2).
wt_bwa_aln	:= "10h"		help Walltime for bwa aln (default: 10h, 10:00:00).
mem_bwa_aln	:= "8G"	 		help Max. memory for bwa aln (default: 8G).
wt_bwa_sam	:= "12h"		help Walltime for bwa sampe/samse (default: 12h, 12:00:00).
mem_bwa_sam	:= "8G"			help Max. memory for bwa sampe/samse (default: 8G).


init_align_bwa()


void init_align_bwa() {

	if ( conf_file_exists() ) {

		param_bwa_aln 	= get_conf_val( param_bwa_aln, 		["BWA_ALN_PARAM", "PARAM_BWA_ALN"] )

		nth_bwa_aln 	= get_conf_val_int( nth_bwa_aln,	["NTHREADS_BWA_ALN", "NTH_BWA_ALN", "CPU_BWA_ALN"] )
		wt_bwa_aln 	= get_conf_val( wt_bwa_aln, 		["WALLTIME_BWA_ALN", "WT_BWA_ALN", "TIMEOUT_BWA_ALN"] )
		mem_bwa_aln 	= get_conf_val( mem_bwa_aln, 		["MEMORY_BWA_ALN", "MEM_BWA_ALN"] )
	}

	if ( v ) { // verbose
		print("\n")		
		print_if_not_default( "Param. for bwa aln", "$param_bwa_aln","-q 5 -l 32 -k 2")
		print_if_not_default( "# threads (bwa aln)", "$nth_bwa_aln", 2)
		print_if_not_default( "Walltime (bwa aln)", "$wt_bwa_aln", "10h")
		print_if_not_default( "Max. memory (bwa aln)", "$mem_bwa_aln", "8G")
		print_if_not_default( "Walltime (bwa sam)", "$wt_bwa_sam", "12h" )
		print_if_not_default( "Max. memory (bwa sam)", "$mem_bwa_sam", "8G" )
	}	
}

/*		
final_stage : align finishes at final_stage.
	(bam, nodup_bam, tag, xcor)

data type : align starts from a given type.
	(fastq, bam, nodup_bam, tag)

_PE : for paired end data
*/

string[] _align_bwa( string[] data, string type, string final_stage, bool is_ctl, string out_dir, string info ) {

	string bam, qc // make bam and map_qc with using bwa
	if ( is_data_type_before( type, "bam" ) ) {

		fastq := data[0]

		(bam, qc) = _bwa( fastq, out_dir, info )
		wait

		if ( final_stage.toLower() == "bam" ) return [bam, qc]
	}

	string nodup_bam, nodup_bam_idx, nmsrt_bam // remove dupes and make nodup.bam and name sorted bam
	if ( is_data_type_before( type, "nodup_bam" ) ) {

		if ( type.toLower() == "bam" ) bam = data[0]

		(nodup_bam, nodup_bam_idx) = _bam_to_nodup_bam( bam, out_dir, info )
		wait

		if ( final_stage.toLower() == "nodup_bam" ) return [nodup_bam]
	}

	string tag // make tagalign
	if ( is_data_type_before( type, "tag" ) ) {

		if ( type.toLower() == "nodup_bam" ) nodup_bam = data[0]

		tag = _bam_to_tag( nodup_bam, out_dir, info )
		wait

		if ( final_stage.toLower() == "tag" ) return [tag]
	}

	string xcor_score, xcor_plot // make cross correlation score and plot	
	if ( is_data_type_before( type, "xcor" ) ) {

		if ( type.toLower() == "tag" ) tag = data[0]

		subsampled_tag := _subsample_tag( tag, out_dir, info )
		wait

		(xcor_score, xcor_plot) = _xcor( subsampled_tag, out_dir, info )
		wait

		save_data( "$out_dir/data_fraglen_$info", [ get_fraglen(xcor_score) ] )

		if ( final_stage.toLower() == "xcor" ) return [xcor_score, xcor_plot]
	}

	string tag_pr1, tag_pr2 // make self pseudo replicate

	if ( !is_ctl ) (tag_pr1, tag_pr2) = _spr( tag, out_dir, info )

	save_data( "$out_dir/data_tag_$info", [tag, tag_pr1, tag_pr2] )

	return [tag, tag_pr1, tag_pr2]
}

string[] _align_bwa_PE( string[] data, string type, string final_stage, bool is_ctl, string out_dir, string info ) {

	string bam, qc // make bam and map_qc with using bwa
	if ( is_data_type_before( type, "bam" ) ) {

		fastq1 := data[0]
		fastq2 := data[1]
		(bam, qc) = _bwa_PE( fastq1, fastq2, out_dir, info )
		wait

		if ( final_stage.toLower() == "bam" ) return [bam, qc]
	}

	string nodup_bam, nodup_bam_idx // remove dupes and make nodup.bam
	if ( is_data_type_before( type, "nodup_bam" ) ) {

		if ( type.toLower() == "bam" ) bam = data[0]

		(nodup_bam, nodup_bam_idx) = _bam_to_nodup_bam_PE( bam, out_dir, info )
		wait

		if ( final_stage.toLower() == "nodup_bam" ) return [nodup_bam]
	}

	string tag // make tagalign
	if ( is_data_type_before( type, "tag" ) ) {

		if ( type.toLower() == "nodup_bam" ) nodup_bam = data[0]

		tag = _bam_to_tag( nodup_bam, out_dir, info )
		wait

		if ( final_stage.toLower() == "tag" ) return [tag]
	}

	string bedpe, xcor_score, xcor_plot // make cross correlation score and plot	
	if ( is_data_type_before( type, "xcor" ) ) {

		string subsampled_tag

		if ( type.toLower() == "tag" ) {
			tag = data[0]

			subsampled_tag = _subsample_tag( tag, out_dir, info )
			wait
		}
		else {
			nmsrt_bam := _bam_to_nmsrt_bam( nodup_bam, out_dir, info )
			wait

			bedpe = _bam_to_bedpe( nmsrt_bam, out_dir, info )
			wait 

			subsampled_tag = _subsample_bedpe( bedpe, out_dir, info )
			wait
		}

		(xcor_score, xcor_plot) = _xcor( subsampled_tag, out_dir, info )
		wait

		save_data( "$out_dir/data_fraglen_$info", [ get_fraglen(xcor_score) ] )

		if ( final_stage.toLower() == "xcor" ) return [xcor_score, xcor_plot]
	}

	string tag_pr1, tag_pr2 // make self pseudo replicate	

	if ( !is_ctl ) {
		if ( type.toLower() == "tag" ) { // if started from tag, we don't have bedpe file, use SE version
			(tag_pr1, tag_pr2) = _spr( tag, out_dir, info )	
		}
		else {
			(tag_pr1, tag_pr2) = _spr_PE( bedpe, out_dir, info )
		}
	}

	save_data( "$out_dir/data_tag_$info", [tag, tag_pr1, tag_pr2] )
	return [tag, tag_pr1, tag_pr2]
}

string[] _bwa( string fastq, string out_dir, string info ) {

	prefix 	:= change_dir( remove_ext( fastq, "fastq" ), out_dir )
	bam 	:= "$prefix.bam"
	qc 	:= "$prefix.flagstat.qc"

	in 	:= [ fastq ]
	out 	:= [ bam, qc ]

	if ( out <- in ) { // compare file timestamps of in and out (to check if job is already done or not)
		sai := _bwa_aln( fastq, out_dir, info )
		wait

		_bwa_sam( fastq, sai, out_dir, info )
		wait

		sai.rm() // delete intermediate file sai		
	}

	return out
}

string[] _bwa_PE( string fastq1, string fastq2, string out_dir, string info ) {

	//prefix 	:= "$out_dir/" + concat_basename_wo_ext( fastq1, fastq2, "fastq" )
	prefix 	:= change_dir( remove_ext( fastq1, "fastq" ), out_dir ) + ".PE2SE"

	bam 	:= "$prefix.bam"
	qc 	:= "$prefix.flagstat.qc"

	in 	:= [ fastq1, fastq2 ]
	out 	:= [ bam, qc ]

	if ( out <- in ) { // compare file timestamps of in and out (to check if job is already done or not)

		// parallel jobs
		sai1 := _bwa_aln( fastq1, out_dir, info ) 
		sai2 := _bwa_aln( fastq2, out_dir, info )

		wait

		_bwa_sam_PE( fastq1, fastq2, sai1, sai2, out_dir, info )
		wait
		
		sai1.rm() // delete intermediate file sai1, sai2
		sai2.rm()
	}

	return out
}

string _bwa_aln( string fastq, string out_dir, string info ) {

	prefix 	:= change_dir( remove_ext( fastq, "fastq" ), out_dir )

	sai 	:= "$prefix.sai"	
	
	in 	:= [ fastq ]
	out 	:= sai

	task( taskName:= "bwa_aln "+info, \
		cpus:=nth_bwa_aln, mem:=parse_mem(mem_bwa_aln), timeout:=parse_time(wt_bwa_aln), \
		out<-in ) {

		sys $SHCMD_INIT

		//# Map reads to create raw SAM file
		sys bwa aln $param_bwa_aln -t $nth_bwa_aln $bwa_idx $fastq > $sai
	}

	return out
}

string[] _bwa_sam( string fastq, string sai, string out_dir, string info ) {

	prefix 	:= change_dir( remove_ext( fastq, "fastq" ), out_dir )

	bam 	:= "$prefix.bam"
	qc	:= "$prefix.flagstat.qc"

	in 	:= [ fastq, sai ]
	out 	:= [ bam, qc ]

	task( taskName:= "bwa_sam "+info, \
		cpus:=nth, mem:=parse_mem(mem_bwa_sam), timeout:=parse_time(wt_bwa_sam), \
		out<-in ) {

		sys $SHCMD_INIT

		sys bwa samse $bwa_idx $sai $fastq | samtools view -Su - | samtools sort - $prefix
		sys samtools flagstat $bam > $qc
	}

	return out
}

string[] _bwa_sam_PE( string fastq1, string fastq2, string sai1, string sai2, string out_dir, string info ) {

	//prefix 	:= "$out_dir/" + concat_basename_wo_ext( fastq1, fastq2, "fastq" )
	prefix 	:= change_dir( remove_ext( fastq1, "fastq" ), out_dir ) + ".PE2SE"

	sam 	:= "$prefix.sam"
	badcigar:= "$prefix.badReads"
	bam 	:= "$prefix.bam"
	qc	:= "$prefix.flagstat.qc"

	in 	:= [ fastq1, fastq2, sai1, sai2 ]
	out 	:= [ bam, qc ]

	task( taskName:= "bwa_sam_PE "+info, \
		cpus:=nth, mem:=parse_mem(mem_bwa_sam), timeout:=parse_time(wt_bwa_sam), \
		out<-in ) {

		sys $SHCMD_INIT
		
		sys bwa sampe $bwa_idx $sai1 $sai2 $fastq1 $fastq2 | gzip -c > $sam

		//# Remove read pairs with bad CIGAR strings and sort by position                                                                

		//# Find bad CIGAR read names                                                                                                                                                  
		sys zcat $sam \
			| awk 'BEGIN {FS="\t" ; OFS="\t"} ! /^@/ && $6!="*" { cigar=$6; gsub("[0-9]+D","",cigar); n = split(cigar,vals,"[A-Z]"); s = 0; for (i=1;i<=n;i++) s=s+vals[i]; seqlen=length($10) ; if (s!=seqlen) print $1"\t"; }' \
			| sort | uniq > $badcigar

		//# Remove bad CIGAR read pairs
		sys if [ $(cat $badcigar | wc -l) -gt 0 ]; then \
				zcat $sam | grep -v -F -f $badcigar | samtools view -Su - | samtools sort - $prefix; \
			else \
				samtools view -Su $sam | samtools sort - $prefix; \
			fi

		sys samtools flagstat $bam > $qc
		sys rm -f $badcigar $sam
	}

	return out
}

bool is_data_type_before( string type, string stage ) {

	if ( stage.toLower() == "bam" ) {
		return is_in_array( type, ["fastq"] )
	}
	else if ( stage.toLower() == "nodup_bam" ) {
		return is_in_array( type, ["fastq","bam"] )
	}
	else if ( stage.toLower() == "tag" || stage.toLower() == "tagalign" ) {
		return is_in_array( type, ["fastq","bam","nodup_bam"] ) 	
	}
	else if ( stage.toLower() == "xcor" ) {
		return is_in_array( type, ["fastq","bam","nodup_bam","tag","tagalign"] )
	}

	return false
}
